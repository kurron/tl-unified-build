apply plugin: 'project-report'
apply plugin: 'groovy'
apply plugin: 'maven'

group = 'org.kurron'

if ( project.properties.containsKey( 'project_version' ) )
{
    logger.quiet "Using specifed project_version of $project_version"
    status = 'release'
}
else
{
    ext.project_version = '1.0.0-SNAPSHOT'
    logger.quiet "project_version not specified.  Using default version of $project_version"
    status = 'integration'
}
project.version = project.project_version

ext.isSnapshot = version.endsWith('-SNAPSHOT')
ext.publicationUrl = "http://192.168.254.81:81/artifactory/mods-release-local"
ext.publicationUsername = 'SVNBuild'
ext.publicationPassword = 'Snoopy*09'

repositories {
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
}

defaultTasks 'projectReport', 'build'

if ( isSnapshot )
{
    logger.quiet 'Snapshot revision detected. Configuring for local installation.'
    project.defaultTasks.add( 'install' )
}
else
{
    logger.quiet 'Release revision detected. Configuring for publication.'
    uploadArchives {
        repositories {
            maven {
                description 'Sandbox area of the repository to test publishing to'
                url = publicationUrl
                credentials {
                    username = publicationUsername
                    password = publicationPassword
                }
            }
        }
    }
    defaultTasks.add( 'uploadArchives' )
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
    description = 'Generate the Gradle wrapper scripts'
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

